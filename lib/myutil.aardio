//myutil 工具库
namespace myutil{
	protectButton=function(button,func){
		button.oncommand= lambda(...){
			owner.disabled=true;
			func(...);
			owner.disabled=false;
		}
	}
	settings=null;
	import fsys.table;
	get_setting=function(path="/config.table"){
		if(not self.settings){
			self.settings = fsys.table(path);
			self.settings.mix(
				data_path="/mods",
				game_path="",
				db_path="/modinfo.db",
				install_dir=null,
				mod_path="\data\game\Game\Content\Paks\~mod",
				use_colors={},
				ui={
					delete_file=false;
					check_once=false;
					language="chinese"
				},
				default_mod_template={
					file_name="文件名称";
					character_num="角色名称";
					web="mod网址";
					describe="mod描述";
				}
			)
		}
		return self.settings;
	}
	save_setting=function(){
		self.settings.save();
	}
	
	import io;
	import string;
	use_mod=function(from,to,ext=".pak"){
		if(from&&to){
			if(io.exist(to)){
				return fsys.rename(to,to++ext);
			}elseif(io.exist(from)){
				fsys.createParentDir(to,false);
				return fsys.copy(from,to++ext);
			}else {
				self[["dlg"]].error_dlg(string.format("路径不存在:{%s}",from));
			}
		}
		return false;
	}
	
	disable_mod=function(path,ext=".pak"){
		if(path){
			true_path=path++ext;
			if(io.exist(true_path)){
				return fsys.rename(true_path,path);
			}else {
				self.dlg.error_dlg(string.format("路径不存在:{%s}",true_path));
			}
		}
		return false; 
	}
	
	//设置语言
	set_language = function(form, lang){
		if(lang=="english"){
			form.groupbox.text="Mods";
			form.static2.text="Game path:";
			form.delete_file_checkbox.text="delete files";
			form.ask_once_checkbox.text="confirm once";
			form.enable_button.text="enable";
			form.disable_button.text="disable"
			form.upadte_button.text="update file";
			form.add_button.text="add";
			form.remove_button.text="remove";
			form.btnIFileOpenDir.text="open";
		}else {
			form.groupbox.text="模组";
			form.static2.text="游戏目录：";
			form.delete_file_checkbox.text="删除文件";
			form.ask_once_checkbox.text="单次确认";
			form.enable_button.text="启用";
			form.disable_button.text="禁用"
			form.upadte_button.text="更新文件";
			form.add_button.text="添加";
			form.remove_button.text="删除";
			form.btnIFileOpenDir.text="打开";
		}
		
	}
	
	namespace dlg{
		import win.dlg.message;
	    dlg=null;
	    init=function(phwnd){
	    	if(not dlg){
	    		self.dlg=win.dlg.message(phwnd);
	    		self.dlg.bgcolor=..gdi.RGB(232,232,232);
	    		self.dlg.textColor=..gdi.RGB(20,20,20);
	    		self.dlg.buttonStyle = { 
	    			color = {  
	    				hover = 0xFF40826D;
	    				active = 0xFF2E8B57; 
	    				default = 0xFF3CB371;
	    			} 
	    			border = {
	    				default = {width=0;}
	    				hover = { bottom = 1;color= 0xF0FFFFFF; }
	    				focus = { bottom = 1;color= 0xF0FFFFFF; }
	    				active = { bottom = 1;color= 0x30FFFFFF; }
	    			} 
	    		}
	    		self.dlg.titlebarStyle = { 
	    			color = { 
	    				hover = 0xFF40826D;
	    				active = 0xFF2E8B57; 
	    				default = 0xFF3CB371;
	    			}
	    		}
	    		//self.dlg.iconColor=..gdi.RGB(20,20,20);
	    	}
	    }
	    error_dlg=function(msg){
	    	self.dlg.err(msg);
	    }
	    info_dlg=function(msg){
	    	self.dlg.info(msg);
	    }
	    success_dlg=function(msg){
	    	self.dlg.ok(msg);
	    }
	    ask_dlg=function(msg){
	    	return self.dlg.ask(msg); 
	    }
	    	
	}
	
	
}
namespace myutil{
}
